struct Time {
  int hour;
  int minute;
  int second;
}


harness void main(int x){

  Time t = new Time(hour=0, minute=0, second=58);
  Time _t = tick(t);
  assert _t.hour == 0;
  assert _t.minute == 0;
  assert _t.second == 59;

  Time t2 = new Time(hour=0, minute=58, second=59);
  Time _t2 = tick(t2);
  assert _t2.hour == 0;
  assert _t2.minute == 59;
  assert _t2.second == 0;

  Time t3 = new Time(hour=22, minute=59, second=59);
  Time _t3 = tick(t3);
  assert _t3.hour == 23;
  assert _t3.minute == 0;
  assert _t3.second == 0;

  Time t4 = new Time(hour=23, minute=59, second=59);
  Time _t4 = tick(t4);
  assert _t4.hour == 0;
  assert _t4.minute == 0;
  assert _t4.second == 0;

}

Time tick(Time t) {

  int _second = t.second + ??;
  int newSecond = _second % ??;
  int minuteInc = _second / ??;

  int _minute = t.minute + minuteInc;
  int newMinute = _minute % ??;
  int hourInc = _minute / ??;

  int newHour = (t.hour + hourInc) % ??;

  Time newTime = new Time(hour=newHour, minute=newMinute, second=newSecond);
  return newTime;
}
